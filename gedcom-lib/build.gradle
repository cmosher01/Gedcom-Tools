plugins {
    id 'application'
    id 'java-library'
    id 'ca.coglinc.javacc' version '2.4.0'
    id 'signing'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':mopper')
    implementation group: 'nu.mine.mosher.gedcom', name: 'ansel', version: 'latest.integration'
    implementation group: 'com.github.albfernandez', name: 'juniversalchardet', version: 'latest.release'

    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: 'latest.release'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: 'latest.release'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: 'latest.release'
}

application {
    getMainClass().set 'nu.mine.mosher.gedcom.Gedcom'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of 11
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(Javadoc) {
    failOnError false
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from project.components.java
            pom {
                name = 'GEDCOM Library (for Java)'
                description = 'Java library for parsing a GEDCOM file'
                url = 'https://github.com/cmosher01/gedcom-tools'
                inceptionYear = '2004'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'cmosher01'
                        name = 'Christopher Alan Mosher'
                        email = 'cmosher01@gmail.com'
                        url = 'https://mosher.mine.nu'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/cmosher01/gedcom-tools.git'
                    developerConnection = 'scm:git:ssh://github.com/cmosher01/gedcom-tools.git'
                    url = 'http://github.com/cmosher01/gedcom-tools'
                }
            }
        }
    }
}
tasks.assemble.dependsOn tasks.generatePomFileForMavenPublication

signing {
    sign publishing.publications.maven
}
tasks.assemble.dependsOn tasks.signMavenPublication

publishing {
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

version = '1.3.9'
