plugins {
    id 'application'
    id 'java-library'
    id 'ca.coglinc.javacc' version '2.4.0'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: 'nu.mine.mosher.mopper', name: 'mopper', version: 'latest.integration'
    implementation group: 'nu.mine.mosher.gedcom', name: 'gedcom-ansel', version: 'latest.integration'
    implementation group: 'com.github.albfernandez', name: 'juniversalchardet', version: 'latest.release'

    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: 'latest.release'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: 'latest.release'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: 'latest.release'
}

application {
    mainClass = 'nu.mine.mosher.gedcom.Gedcom'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of 11
        vendor = JvmVendorSpec.AMAZON
    }
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) { // generate "sources" jar file
    from sourceSets.main.allJava
    archiveClassifier.set 'sources'
}
tasks.assemble.dependsOn tasks.sourcesJar

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
tasks.assemble.dependsOn tasks.generatePomFileForMavenPublication

version = '1.3.9'
